pkgname='spot-client'
_pkgname='spot'
pkgver='0.2.0'
pkgrel='1'
pkgdesc='Gtk/Rust native Spotify client'
arch=('x86_64')
url="https://github.com/xou816/spot"
license=('MIT')
depends=('alsa-lib' 'glib2' 'gtk4' 'libhandy' 'libpulse' 'openssl' 'libadwaita' 'pango' 'cairo' 'graphene' 'glibc')
optdepends=('org.freedesktop.secrets')
makedepends=('cargo' 'meson>=0.50.0')
checkdepends=('appstream-glib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/xou816/spot/archive/${pkgver}.tar.gz")
sha512sums=('04930c3be37b45ad9dcac3448d6e3154f597a3caa945fc22f82e552d46960ec869e67d43b7c8117e2ecc6e97e2bfd6d7d0a8a43926467d72c10a706bc137daf7')
validpgpkeys=() # TODO https://github.com/xou816/spot/issues/283

build() {
    arch-meson -Doffline='false' "${_pkgname}-${pkgver}" build
    meson compile -C build
}

check() {
    meson test -C build --timeout-multiplier -1
}

package() {
    meson install -C build --destdir "${pkgdir}"

    install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

