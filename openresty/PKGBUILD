_cfgdir=/etc/openresty
_tmpdir=/var/lib/openresty
_pkgname=ngx_openresty
_prefix=/opt/openresty

# add nginx-push-stream-module
_pushstreamver=0.4.1

pkgname=openresty
pkgver=1.7.7.2
pkgrel=1
pkgdesc="a powerful web app server by extending nginx"
arch=('i686' 'x86_64')
url="http://openresty.org/"
license=('BSD')
depends=('readline' 'pcre' 'zlib' 'openssl')
backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/openresty)
#install=
source=(http://openresty.org/download/$_pkgname-$pkgver.tar.gz
        service
        logrotate)
        # nginx-push-stream-module-${_pushstreamver}.tar.gz::https://github.com/wandenberg/nginx-push-stream-module/archive/${_pushstreamver}.tar.gz
sha256sums=('46239c72a1059ff703c119512d56eb4a82c89060701371eebc01b351f31efcad'
            '41290a052042701f29b30fadf9ed828f5730ea43aac1282ee2b6c85e3b89651a')
sha256sums=('5cd6db20c3b4e9f128eda7e7b31810f8b278ea0644fdd9d2f51b34ad2b356298'
            '77d5dc4804cb10bdac15d8fa82330de8b5a99fe9a9f8b3f59d218f544c53b9e8'
            '41290a052042701f29b30fadf9ed828f5730ea43aac1282ee2b6c85e3b89651a')
            # '553584f557a3faec73702550e7e1417cbc0021f4f98468cc83e61e9d94def5cc'

build() {
    cd $_pkgname-$pkgver
    ./configure \
        --prefix=$_prefix \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/openresty \
        --pid-path=/run/openresty.pid \
        --lock-path=/run/lock/openresty.lock \
        --user=http \
        --group=http \
        --http-log-path=/var/log/openresty/access.log \
        --error-log-path=stderr \
        --http-client-body-temp-path=/var/lib/openresty/client-body \
        --http-proxy-temp-path=/var/lib/openresty/proxy \
        --http-fastcgi-temp-path=/var/lib/openresty/fastcgi \
        --http-scgi-temp-path=/var/lib/openresty/scgi \
        --http-uwsgi-temp-path=/var/lib/openresty/uwsgi \
        --with-imap \
        --with-imap_ssl_module \
        --with-ipv6 \
        --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_spdy_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --with-http_addition_module \
        --with-http_degradation_module \
        --with-http_flv_module \
        --with-http_mp4_module \
        --with-http_secure_link_module \
        --with-http_sub_module \
        --with-luajit
        # --add-module=../nginx-push-stream-module-${_pushstreamver}
    make
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    sed -e 's|\<user\s\+\w\+;|user html;|g' \
        -e '44s|html|/usr/share/openresty/html|' \
        -e '54s|html|/usr/share/openresty/html|' \
        -i "$pkgdir"/etc/openresty/nginx.conf
    rm "$pkgdir"/etc/openresty/*.default

    install -d "$pkgdir"/var/lib/openresty
    install -dm700 "$pkgdir"/var/lib/openresty/proxy

    chmod 750 "$pkgdir"/var/log/openresty
    chown http:log "$pkgdir"/var/log/openresty

    install -d "$pkgdir"/usr/share/openresty
    mv "$pkgdir"/opt/openresty/nginx/html/ "$pkgdir"/usr/share/openresty
    rmdir "$pkgdir"/opt/openresty/nginx

    install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/openresty
    install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/openresty.service
    # install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rmdir "$pkgdir/run"
}

# vim:set ts=4 sw=4 et:
