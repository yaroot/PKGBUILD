pkgname=ttf-google-fonts-git
pkgver=20150316
pkgrel=2
pkgdesc="TrueType fonts from the Google Fonts project"
arch=('any')
url="https://github.com/google/fonts"
license=('various')
# About why "cantarell-fonts" is a dependency see comment in package() function.
depends=('cantarell-fonts'
         'fontconfig'
         'xorg-fonts-encodings'
         'xorg-mkfontdir'
         'xorg-mkfontscale'

         'ttf-droid'
         'ttf-fira-sans'
         'ttf-fira-mono'
         'ttf-inconsolata'
         'adobe-source-code-pro-fonts'
         'adobe-source-sans-pro-fonts'
         'adobe-source-serif-pro-fonts'
         )
makedepends=('git')
conflicts=(
           'googlefontdirectory'
           'jsmath-fonts'
           'lohit-fonts'
           'oldstand-font'
           'openarch_fonts'
           'otf-bitter'
           'otf-goudy'
           'ttf-andika'
           'ttf-anonymous-pro'
           'ttf-cantarell'
           'ttf-cardo'
           'ttf-chromeos-fonts'
           'ttf-comfortaa'
           'ttf-google-fonts-hg'
           'ttf-google-webfonts-git'
           'ttf-google-webfonts-hg'
           'ttf-google-webfonts'
           'ttf-kimberly_geswein_print'
           'ttf-lato'
           'ttf-lekton'
           'ttf-medievalsharp'
           'ttf-nova'
           'ttf-oldstandard'
           'ttf-opensans'
           'ttf-oxygen'
           'ttf-oxygen-git'
           'ttf-pt-mono'
           'ttf-pt-sans'
           'ttf-ptsans'
           'ttf-roboto'
           'ttf-roboto-mono'
           'ttf-sil-fonts'
           'ttf-sortsmillgoudy'
           'ttf-source-code-pro'
           'ttf-source-sans-pro'
           'ttf-ubuntu-font-family'
           'ttf-vollkorn')
provides=("${conflicts[@]}" 'ttf-font')
source=("git://github.com/google/fonts.git")
md5sums=('SKIP')
install=font.install

# git variables
_gitname="fonts"

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/usr/share/fonts/TTF"
  find . -type f -name \*.ttf -exec install -Dm644 '{}' \
    "$pkgdir/usr/share/fonts/TTF" \;

  # Remove excluded fonts.
  find "${pkgdir}/usr/share/fonts/" -type f -name "Cantarell-*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "Droid*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "Fira*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "Inconsolata-*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "Noto*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "SourceCodePro*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "SourceSansPro*.ttf" -delete
  find "${pkgdir}/usr/share/fonts/" -type f -name "SourceSerifPro*.ttf" -delete
}
